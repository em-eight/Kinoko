cmake_minimum_required(VERSION 3.10)
project(Kinoko CXX)

# Compiler and flags
set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(COMMON_CXX_FLAGS
    -fno-asynchronous-unwind-tables
    -fno-exceptions
    -fno-rtti
    -fshort-wchar
    -fstack-protector-strong
    -Wall
    -Werror
    -Wextra
    -Wno-delete-non-virtual-dtor
    -Wno-packed-bitfield-compat
    -Wsuggest-override
)

set(GAME_CXX_FLAGS
    -DKINOKO_EGG_OPERATOR_NEW
)

set(RK_INCLUDE_DIRS
    include
    source
)

# Source files
file(GLOB_RECURSE SOURCE_FILES CONFIGURE_DEPENDS ${CMAKE_SOURCE_DIR}/**/*.cc)

set(GAME_SOURCE_FILES)
set(TEST_SOURCE_FILES)

# Loop through all source files and categorize them
foreach(FILE ${SOURCE_FILES})
    # Check if the file is in the 'test' folder
    if(FILE MATCHES "/test/")
	list(APPEND TEST_SOURCE_FILES ${FILE})
    else()
        list(APPEND GAME_SOURCE_FILES ${FILE})
    endif()
endforeach()

add_library(libkinoko ${GAME_SOURCE_FILES})
target_include_directories(libkinoko SYSTEM
    PRIVATE ${RK_INCLUDE_DIRS}
)
target_include_directories(libkinoko
    INTERFACE ${RK_INCLUDE_DIRS}
)
target_compile_options(libkinoko PRIVATE ${COMMON_CXX_FLAGS} ${GAME_CXX_FLAGS})
target_compile_features(libkinoko PUBLIC cxx_std_23)
target_link_options(libkinoko PRIVATE "-Wl,--version-script=${CMAKE_SOURCE_DIR}/hide.map")

# Create targets
add_executable(kinoko ${TEST_SOURCE_FILES})
target_link_libraries(kinoko libkinoko)
target_compile_options(kinoko PRIVATE ${COMMON_CXX_FLAGS})

# Add a custom target to generate testCases.json
set(TEST_JSON ${CMAKE_CURRENT_SOURCE_DIR}/testCases.json)
set(TEST_BIN ${CMAKE_CURRENT_BINARY_DIR}/testCases.bin)
add_custom_command(
    OUTPUT ${TEST_BIN}  # The file generated by this rule
    DEPENDS ${TEST_JSON}  # Dependency
    COMMAND python ${CMAKE_CURRENT_SOURCE_DIR}/tools/generate_tests.py ${TEST_JSON} ${TEST_BIN}
    COMMENT "Running generate_tests.py to create test cases"
)
add_custom_target(
    GenerateTestCases
    ALL
    DEPENDS ${TEST_BIN}  # Ensures this target depends on the generated file
)
